import com.amazonaws.services.dynamodbv2.model.AttributeDefinition
import com.amazonaws.services.dynamodbv2.model.KeySchemaElement
import com.amazonaws.services.dynamodbv2.model.KeyType
import com.amazonaws.services.dynamodbv2.model.ScalarAttributeType
import com.nike.pdm.localstack.aws.dynamodb.CreateDynamoDbTableTask

plugins {
    id "java"
    id "application"
    id "com.avast.gradle.docker-compose"    version "0.14.3"
    id "com.nike.pdm.localstack"            version "0.2.0"
}

// Project Configuration
project.description = "Example of using the dynamo-scan library to scan records from an AWS DynamoDB table."
mainClassName = "example.Application"

dependencies {
    implementation project(":dynamo-scan")
}

// Local Development
dockerCompose {
    useComposeFiles = [ "${projectDir}/localstack/localstack-docker-compose.yml" ]
}

run.dependsOn('startLocalStack')

task setupLocalTable(type: CreateDynamoDbTableTask) {
    tableName = 'catalog.products'
    keySchema = [
        new KeySchemaElement("id", KeyType.HASH)
    ]
    attributeDefinitions = [
        new AttributeDefinition("id", ScalarAttributeType.S)
    ]
    initializer = 'example.dynamodb.ProductTableInitializer'
}
